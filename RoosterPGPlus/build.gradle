buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.6.3'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 19
    buildToolsVersion "17.0.0"

    signingConfigs {
        release {
            storeFile file("C:\\Users\\Thomas\\Desktop\\longkey")
            keyAlias "ThomasdH"
            storePassword "notYourRealPassword"
            keyPassword "notYourRealPassword"
        }
    }

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 19
    }
}

task askForPasswords << {
    // Must create String because System.readPassword() returns char[]
    // (and assigning that below fails silently)
    def storePw = new String(System.console().readPassword("Keystore password: "))
    def keyPw  = new String(System.console().readPassword("Key password: "))

    android.signingConfigs.release.storePassword = storePw
    android.signingConfigs.release.keyPassword = keyPw
}

tasks.whenTaskAdded { theTask ->
    if (theTask.name.equals("packageRelease")) {
        theTask.dependsOn "askForPasswords"
    }
}

dependencies {
	compile 'com.android.support:support-v4:19.0.0'
	compile 'com.android.support:gridlayout-v7:19.0.0'
	compile 'com.android.support:appcompat-v7:19.0.0'
    compile files('libs/libGoogleAnalyticsServices.jar')
}
